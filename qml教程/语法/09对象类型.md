QML对象类型是可以从中实例化QML对象的类型。

用语法术语来说，QML对象类型是一种可以用来声明对象的方法，方法是指定*类型名称，*后跟一组包含该对象属性的花括号。这与*基本类型*不同，*基本类型*不能以相同的方式使用。例如，[Rectangle](https://doc.qt.io/qt-5/qml-qtquick-rectangle.html)是QML对象类型：可用于创建`Rectangle`类型对象。使用诸如`int`和的原始类型无法完成此操作，该原始类型`bool`用于保存简单数据类型而不是对象。

```js
//对象类型 正确
Rectangle
{
    color:"red"
}
//基础类型 错误
int{}
```

## 定义对象类型的方法

* 通过创建一个qml文件来定义自定义QML对象类型
* 通过从C ++定义QML类型并向QML引擎注册该类型来[定义](https://doc.qt.io/qt-5/qtqml-cppintegration-definetypes.html)对象类型

###  从QML定义对象类型

#### 通过QML文档定义对象类型

插件编写者和应用程序开发人员可以提供定义为QML文档的类型。当QML文档对QML导入系统可见时，它定义一种类型，该类型由文件名减去文件扩展名标识。

因此，如果存在一个名为“ MyButton.qml”的QML文档，它将提供“ MyButton”类型的定义，该定义可在QML应用程序中使用。

#### 用组件定义匿名类型

在QML中创建对象类型的另一种方法是使用[Component](https://doc.qt.io/qt-5/qml-qtqml-component.html)类型。这允许在QML文档中内联定义类型，而不是在文件中使用单独的`.qml`文件。

```js
Item {
    id: root
    width: 500; height: 500

    Component {
        id: myComponent
        Rectangle { width: 100; height: 100; color: "red" }
    }

    Component.onCompleted: {
        myComponent.createObject(root)
        myComponent.createObject(root, {"x": 200})
    }
}
```

### 从C ++定义对象类型

C ++插件编写者和应用程序开发人员可以通过Qt QML模块提供的API注册C ++定义的类型。有多种注册功能，每种注册功能都可以满足不同的用例。



## 通过QML文档定义对象类型

QML的核心功能之一是，它可以通过QML文档以轻量级的方式轻松定义QML对象类型，以满足各个QML应用程序的需求。标准的[Qt Quick](https://doc.qt.io/qt-5/qtquick-index.html)模块提供了各种类型，例如[Rectangle](https://doc.qt.io/qt-5/qml-qtquick-rectangle.html)，[Text](https://doc.qt.io/qt-5/qml-qtquick-text.html)和[Image](https://doc.qt.io/qt-5/qml-qtquick-image.html)来构建QML应用程序。除此之外，您还可以轻松定义自己的QML类型，以在应用程序中重用。这种创建自己的类型的能力构成了任何QML应用程序的基础。

### 命名自定义QML对象类型

要创建对象类型，应将QML文档放入名为* .qml*的文本文件中，其中**是所需的类型名称。类型名称具有以下要求：

- 它必须由字母数字字符或下划线组成。
- 它必须以大写字母开头。

然后，引擎会自动将该文档识别为QML类型的定义。另外，在解析QML类型名称时，与引擎在直接目录中搜索时一样，以这种方式定义的类型将自动提供给同一目录中的其他QML文件。

### 定义一个QMl类型

例如，下面是一个带有MouseArea的Rectangle的文档。该文档已保存到名为SquareButton.qml的文件中：

```js
// SquareButton.qml
import QtQuick 2.0

Rectangle {
    property int side: 100
    width: side; height: side
    color: "red"

    MouseArea {
        anchors.fill: parent
        onClicked: console.log("Button clicked!")
    }
}
```

由于文件名为SquareButton.qml，因此同一目录内的任何其他QML文件现在都可以将其用作名为SquareButton的类型。例如，如果在同一目录中有一个myapplication.qml文件，则它可以引用SquareButton类型：

```js
// myapplication.qml
import QtQuick 2.0

SquareButton {}
```

如SquareButton.qml中所定义，这将创建一个具有内部MouseArea的100 x 100红色矩形。当引擎加载此myapplication.qml文档时，它会将SquareButton.qml文档作为组件加载并实例化以创建SquareButton对象。

### 内联组件

有时，为类型创建新文件可能很不方便，例如在多个视图中重用小型委托时。如果您实际上不需要公开类型，而只需要创建一个实例，则可以选择Component。但是，如果要使用组件类型声明的属性，或者要在多个文件中使用它，则不能选择Component。

内联组件在文件内部声明一个新组件的语法是

```js
//qt 5.15 后的语法,之前版本不支持
component <component name> : BaseType {
    // declare properties and bindings here
}
```

### 自定义类型的可访问属性

.qml文件中的根对象**定义了可用于QML类型的属性**。此根对象的所有属性，信号和方法(无论是自定义声明的还是来自根对象的QML类型)都可以从外部访问，并且可以针对此类型的对象进行读取和修改。

```js
// SquareButton.qml
import QtQuick 2.0

Rectangle {
    id: root

    property bool pressed: mouseArea.pressed

    signal buttonClicked(real xPos, real yPos)

    function randomizeColor() {
        root.color = Qt.rgba(Math.random(), Math.random(), Math.random(), 1)
    }

    property int side: 100
    width: side; height: side
    color: "red"

    MouseArea {
        id: mouseArea
        anchors.fill: parent
        onClicked: root.buttonClicked(mouse.x, mouse.y)
    }
}

//任何SquareButton对象都可以利用已添加到根Rectangle中的pressed属性，buttonClicked信号和randomizeColor（）方法：

// application.qml
import QtQuick 2.0

SquareButton {
    id: squareButton

    onButtonClicked: {
        console.log("Clicked", xPos, yPos)
        randomizeColor()
    }

    Text { text: squareButton.pressed ? "Down" : "Up" }
}
```

## 从C ++定义QML类型

